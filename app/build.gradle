plugins {
    id 'java'
    id 'jacoco'
    id 'jacoco-report-aggregation'

    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.15'
    id 'org.beryx.jlink' version '2.25.0'
}

group = 'com.vanxacloud.appstudio'
version = project['version']
if (repositories.isEmpty()) {
    repositories {
        mavenCentral()
    }
}

dependencies {
    jacocoAggregation project(':app')
    implementation("org.openjfx:javafx-controls:21.0.4")
    implementation("org.openjfx:javafx-fxml:19")

    implementation("org.controlsfx:controlsfx:11.2.2")
    implementation("com.dlsc.formsfx:formsfx-core:11.6.0") {
        exclude group: "org.openjfx"
    }
    implementation("net.synedra:validatorfx:0.6.3") {
        exclude group: "org.openjfx"
    }
    implementation("org.kordamp.ikonli:ikonli-javafx:12.4.0")
    implementation("eu.hansolo:tilesfx:21.0.9") {
        exclude group: "org.openjfx"
    }

    implementation "fr.brouillard.oss:cssfx:11.4.0"

    implementation "org.apache.commons:commons-lang3:3.20.0"
    implementation("ch.qos.logback:logback-classic:1.5.32")


    testImplementation "org.testfx:testfx-core:4.0.18"
    testImplementation 'org.assertj:assertj-core:3.27.7'
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation('org.junit.jupiter:junit-jupiter')
    testRuntimeOnly('org.junit.platform:junit-platform-launcher')
}

test {
    finalizedBy jacocoTestReport // report is always generated after tests run
    useJUnitPlatform()
}

javafx {
    version = "21.0.6"
    modules = ["javafx.controls", "javafx.fxml", "javafx.web", "javafx.swing", "javafx.media"]
}

application {
    mainModule.set("com.vanxacloud.appstudio.proximity")
    mainClass.set("com.vanxacloud.appstudio.proximity.Launcher")
}

distributions {
    main {
        distributionBaseName = 'proximity'
        distributionClassifier = 'bin'
    }
}


jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    reports {
        xml.required = true
        csv.required = true
        html.required = true
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.80
            }
        }
    }
}

tasks.named('check') {
    dependsOn tasks.named('testCodeCoverageReport', JacocoReport)
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}